plugins {
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.0.9'
  id 'com.palantir.git-version' version '0.12.3'
}

repositories {
  mavenCentral()
  jcenter()

  maven {
    url 'https://oss.sonatype.org/content/repositories/snapshots/'
  }

  maven {
    url "https://nexus.bedatadriven.com/content/groups/public"
  }
}

// Assume a cross-platform Ã¼berjar unless targetOs is set.
String[] os = ["win", "mac", "linux"]

if (project.hasProperty('targetOs')) {
  if ("windows" == targetOs) {
    os = ["win"]
  } else {
    os = [targetOs]
  }
}

javafx {
  version = "16"
  modules = ['javafx.controls', 'javafx.swing']
  configuration = 'compileOnly'
}

dependencies {
  def v_junit = '5.7.1'
  def v_flexmark = '0.62.2'
  def v_jackson = '2.12.2'
  def v_batik = '1.14'
  def v_wheatsheaf = '2.0.1'

  // JavaFX
  implementation 'org.controlsfx:controlsfx:11.1.0'
  implementation 'org.fxmisc.richtext:richtextfx:0.10.6'
  implementation 'org.fxmisc.wellbehaved:wellbehavedfx:0.3.3'
  implementation 'com.miglayout:miglayout-javafx:5.2'
  implementation('com.dlsc.preferencesfx:preferencesfx-core:11.7.0') {
    exclude group: 'org.openjfx'
  }

  // Pure JavaFX File Chooser
  implementation "com.io7m.jwheatsheaf:com.io7m.jwheatsheaf:${v_wheatsheaf}"
  implementation "com.io7m.jwheatsheaf:com.io7m.jwheatsheaf.api:${v_wheatsheaf}"
  implementation "com.io7m.jwheatsheaf:com.io7m.jwheatsheaf.ui:${v_wheatsheaf}"

  // Markdown
  implementation "com.vladsch.flexmark:flexmark:${v_flexmark}"
  implementation "com.vladsch.flexmark:flexmark-ext-definition:${v_flexmark}"
  implementation "com.vladsch.flexmark:flexmark-ext-gfm-strikethrough:${v_flexmark}"
  implementation "com.vladsch.flexmark:flexmark-ext-superscript:${v_flexmark}"
  implementation "com.vladsch.flexmark:flexmark-ext-tables:${v_flexmark}"
  implementation "com.vladsch.flexmark:flexmark-ext-typographic:${v_flexmark}"

  // YAML
  implementation "com.fasterxml.jackson.core:jackson-core:${v_jackson}"
  implementation "com.fasterxml.jackson.core:jackson-databind:${v_jackson}"
  implementation "com.fasterxml.jackson.core:jackson-annotations:${v_jackson}"
  implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${v_jackson}"
  implementation 'org.yaml:snakeyaml:1.27'

  // XML and XSL
  implementation 'com.ximpleware:vtd-xml:2.13.4'
  implementation 'net.sf.saxon:Saxon-HE:10.3'
  //implementation 'xalan:xalan:2.7.2'

  // HTML parsing and rendering
  implementation 'org.jsoup:jsoup:1.13.1'
  implementation 'org.xhtmlrenderer:flying-saucer-core:9.1.20'

  // R
  implementation 'org.renjin:renjin-script-engine:3.5-beta76'

  // SVG
  implementation "org.apache.xmlgraphics:batik-anim:${v_batik}"
  implementation "org.apache.xmlgraphics:batik-awt-util:${v_batik}"
  implementation "org.apache.xmlgraphics:batik-bridge:${v_batik}"
  implementation "org.apache.xmlgraphics:batik-css:${v_batik}"
  implementation "org.apache.xmlgraphics:batik-dom:${v_batik}"
  implementation "org.apache.xmlgraphics:batik-ext:${v_batik}"
  implementation "org.apache.xmlgraphics:batik-gvt:${v_batik}"
  implementation "org.apache.xmlgraphics:batik-parser:${v_batik}"
  implementation "org.apache.xmlgraphics:batik-script:${v_batik}"
  implementation "org.apache.xmlgraphics:batik-svg-dom:${v_batik}"
  implementation "org.apache.xmlgraphics:batik-svggen:${v_batik}"
  implementation "org.apache.xmlgraphics:batik-transcoder:${v_batik}"
  implementation "org.apache.xmlgraphics:batik-rasterizer:${v_batik}"
  implementation "org.apache.xmlgraphics:batik-util:${v_batik}"
  implementation "org.apache.xmlgraphics:batik-xml:${v_batik}"

  // Misc.
  implementation 'org.ahocorasick:ahocorasick:0.6.3'
  implementation 'org.apache.commons:commons-configuration2:2.7'
  implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
  implementation 'javax.validation:validation-api:2.0.1.Final'
  implementation 'org.greenrobot:eventbus:3.2.0'

  // Configuration
  implementation 'org.apache.commons:commons-configuration2:2.7'
  implementation 'commons-beanutils:commons-beanutils:1.9.4'

  // Spelling, TeX, Docking
  implementation fileTree(include: ['**/*.jar'], dir: 'libs')

  def fx = ['controls', 'graphics', 'fxml', 'swing']

  fx.each { fxitem ->
    os.each { ositem ->
      println "org.openjfx:javafx-${fxitem}:${javafx.version}:${ositem}"

      runtimeOnly "org.openjfx:javafx-${fxitem}:${javafx.version}:${ositem}"
    }
  }

  testImplementation "org.junit.jupiter:junit-jupiter-engine:${v_junit}"
  testImplementation "org.junit.jupiter:junit-jupiter-api:${v_junit}"
  testImplementation "org.testfx:testfx-junit5:4.0.16-alpha"
}

compileJava {
  options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

def resourceDir = sourceSets.main.resources.srcDirs[0]

def config = new Properties()
file("${resourceDir}/bootstrap.properties").withInputStream {
  config.load(it)
}

application {
  applicationName = config["application.title"].toLowerCase()
  mainClass.set("com.${applicationName}.Main")

  applicationDefaultJvmArgs = [
      "--add-opens=javafx.controls/javafx.scene.control=ALL-UNNAMED",
      "--add-opens=javafx.controls/javafx.scene.control.skin=ALL-UNNAMED",
      "--add-opens=javafx.graphics/com.sun.javafx.css=ALL-UNNAMED",
      "--add-exports=javafx.graphics/com.sun.javafx.application=ALL-UNNAMED",
  ]
}

version = gitVersion()

def launcherClassName = "com.${applicationName}.Launcher"

def propertiesFile = new File("${resourceDir}/com/${applicationName}/app.properties")
propertiesFile.write("application.version=${version}")

jar {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE

  manifest {
    attributes 'Main-Class': launcherClassName
  }

  from {
    (configurations.runtimeClasspath.findAll { !it.path.endsWith(".pom") }).collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }

  archiveFileName = "${applicationName}.jar"

  exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

distributions {
  main {
    distributionBaseName = applicationName
    contents {
      from { ['LICENSE.md', 'README.md'] }
      into('images') {
        from { 'images' }
      }
    }
  }
}

test {
  useJUnitPlatform()

  doFirst {
    jvmArgs = [
        '--add-exports', "javafx.base/com.sun.javafx.event=ALL-UNNAMED",
    ]

    classpath = files()
  }

  testLogging {
    exceptionFormat = 'full'
  }
}
