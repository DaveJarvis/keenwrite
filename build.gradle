plugins {
  id 'application'
  id 'java'
  id 'java-library-distribution'
  id 'org.openjfx.javafxplugin' version '0.0.8'
}

javafx {
  version = "14"
  modules = [ 'javafx.controls', 'javafx.web' ]
}

repositories {
  mavenCentral()
  jcenter()

  maven {
    url 'https://oss.sonatype.org/content/repositories/snapshots/'
  }

  maven {
    url "https://nexus.bedatadriven.com/content/groups/public"
  }
}

compileJava {
  options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

dependencies {
  implementation 'org.controlsfx:controlsfx:11.0.0'
  implementation 'org.fxmisc.richtext:richtextfx:0.10.5'
  implementation 'org.fxmisc.wellbehaved:wellbehavedfx:0.3.3'
  implementation 'com.miglayout:miglayout-javafx:5.2'
  implementation 'com.vladsch.flexmark:flexmark:0.61.28'
  implementation 'com.vladsch.flexmark:flexmark-ext-tables:0.61.28'
  implementation 'com.vladsch.flexmark:flexmark-ext-superscript:0.61.28'
  implementation 'com.vladsch.flexmark:flexmark-ext-gfm-strikethrough:0.61.28'
  compile 'com.fasterxml.jackson.core:jackson-core:2.9.3'
  compile 'com.fasterxml.jackson.core:jackson-databind:2.9.3'
  compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.3'
  compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.3'
  compile 'org.ahocorasick:ahocorasick:0.4.0'
  compile 'org.yaml:snakeyaml:1.19'
  compile 'com.ximpleware:vtd-xml:2.13.4'
  compile 'net.sf.saxon:Saxon-HE:9.8.0-7'
  implementation 'org.apache.commons:commons-configuration2:2.7'
  implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
  implementation 'de.jensd:fontawesomefx-commons:11.0'
  implementation 'de.jensd:fontawesomefx-fontawesome:4.7.0-11'
  implementation "org.renjin:renjin-script-engine:0.9.2726"
}

version = '1.3.8'
applicationName = 'scrivenvar'
mainClassName = 'com.scrivenvar.Main'
sourceCompatibility = JavaVersion.VERSION_1_8

jar {
  baseName = applicationName
  archiveName = "${applicationName}.jar"
  
  doFirst {
    from {
      configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
  }

  // Remove digital signature files to ensure an executable JAR file.
  exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA' 

  manifest {
    attributes 'Main-Class': mainClassName
    attributes 'Class-Path': configurations.compile.collect {
     'libs/' + it.getName()
    }.join(' ')
  }
}

distributions {
  main {
    baseName = applicationName
    contents {
      from { ['LICENSE.md', 'README.md'] }
      into( 'images' ) {
        from { 'images' }
      }
    }
  }
}
